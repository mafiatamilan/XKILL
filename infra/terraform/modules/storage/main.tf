resource "aws_s3_bucket" "this" {
  for_each = var.buckets

  bucket        = each.key
  force_destroy = each.value.force_destroy

  tags = {
    Name        = each.key
    Environment = var.environment
    Project     = "xkill"
    ManagedBy   = "Terraform"
  }
}

resource "aws_s3_bucket_versioning" "this" {
  for_each = { for k, v in var.buckets : k => v if v.versioning_enabled }

  bucket = aws_s3_bucket.this[each.key].id
  versioning_configuration {
    status = "Enabled"
  }
}

resource "aws_s3_bucket_server_side_encryption_configuration" "this" {
  for_each = var.buckets

  bucket = aws_s3_bucket.this[each.key].id
  rule {
    apply_server_side_encryption_by_default {
      sse_algorithm = "AES256"
    }
  }
}

resource "aws_s3_bucket_public_access_block" "this" {
  for_each = var.buckets

  bucket = aws_s3_bucket.this[each.key].id

  block_public_acls       = true
  block_public_policy     = true
  ignore_public_acls      = true
  restrict_public_buckets = true
}

resource "aws_s3_bucket_lifecycle_configuration" "this" {
  for_each = { for k, v in var.buckets : k => v if v.lifecycle_rule_enabled }

  bucket = aws_s3_bucket.this[each.key].id

  rule {
    id     = "expiration"
    status = "Enabled"

    expiration {
      days = each.value.lifecycle_expiration_days
    }

    noncurrent_version_expiration {
      noncurrent_days = each.value.lifecycle_expiration_days
    }
  }
}
